plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'maven'
    id 'signing'
}

repositories {
    mavenCentral()
//    mavenLocal()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

group = "io.github.scottg489"
archivesBaseName = "unified-diff-parser"
version = "0.5.0-SNAPSHOT"


dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.4'

    implementation 'io.github.scottg489:regex-directed-graph-line-parser:0.3.4'

    testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4'
    testImplementation 'junit:junit:4.12'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allGroovy
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: System.getenv("OSSRH_USERNAME"), password: System.getenv("OSSRH_PASSWORD"))
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: System.getenv("OSSRH_USERNAME"), password: System.getenv("OSSRH_PASSWORD"))
            }

            pom.project {
                name 'unified-diff-parser'
                packaging 'jar'
                description 'Extract metadata from unified diffs'
                url 'https://github.com/ScottG489/unified-diff-parser'

                scm {
                    connection 'git@github.com:ScottG489/unified-diff-parser.git'
                    developerConnection 'git@github.com:ScottG489/unified-diff-parser.git'
                    url 'https://github.com/ScottG489/unified-diff-parser'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'scottg489'
                        name 'Scott Giminiani'
                        email 'scottg489@gmail.com'
                    }
                }
            }
        }
    }
}
