plugins {
    id 'groovy'
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'org.jreleaser' version '1.21.0'
}

repositories {
    mavenCentral()
}

group = "io.github.scottg489"
archivesBaseName = "unified-diff-parser"
version = "0.5.5-SNAPSHOT"


dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.25'

    implementation 'io.github.scottg489:regex-directed-graph-line-parser:0.3.4'

    testImplementation 'org.spockframework:spock-core:1.0-groovy-2.4'
    testImplementation 'junit:junit:4.13.2'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.scottg489'
            artifactId = 'unified-diff-parser'

            from components.java

            pom {
                name = 'unified-diff-parser'
                description = 'Extract metadata from unified diffs'
                url = 'https://github.com/ScottG489/unified-diff-parser'
                inceptionYear = '2016'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'scottg489'
                        name = 'Scott Giminiani'
                    }
                }
                scm {
                    connection = 'git@github.com:ScottG489/unified-diff-parser.git'
                    developerConnection = 'git@github.com:ScottG489/unified-diff-parser.git'
                    url = 'https://github.com/ScottG489/unified-diff-parser'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}


jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = 'public.gpg'
        secretKey = 'private.gpg'
        verify = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
